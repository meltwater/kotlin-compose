import static java.lang.System.getenv
buildscript {
    ext.kotlin_version = '1.0.4'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

task ('wrapper', type:Wrapper) {
    gradleVersion = '3.1'
}

//SNAPSHOT=false and not set is the same thing. SNAPSHOT=true is the only way to build a snapshot
project.version = project.version + ('true' == getenv("SNAPSHOT") ? '-SNAPSHOT' : '')
project.group = 'com.meltwater.docker'


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'com.jfrog.bintray'

logger.info("Project version is set to {}", project.version)


repositories {
    jcenter()
}

dependencies {
    compile "org.slf4j:slf4j-api:1.7.12"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.fasterxml.jackson.core:jackson-core:2.7.2"
    compile "com.fasterxml.jackson.core:jackson-databind:2.7.2"
    compile "org.apache.commons:commons-lang3:3.4"
    compile "commons-io:commons-io:2.4"
    compile "org.apache.maven:maven-artifact:3.0.3"

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'ch.qos.logback:logback-classic:1.1.7'

}

task version(){
    println project.version
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}


publishing {
    publications {
        JavaArtifacts(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
    repositories {
        maven {
            credentials {
                username getenv('MAVEN_USER')
                password getenv('MAVEN_PASSWORD')
            }
            url getenv('MAVEN_URL')
        }
    }
}

bintray {
    user = getenv('BINTRAY_USER')
    key = getenv('BINTRAY_KEY')
    publications = ['JavaArtifacts']
    publish = true
    pkg {
        repo = 'opensource'
        name = 'kotlin-compose'
        userOrg = 'meltwater'
        licenses = ['MIT']
        vcsUrl = 'git@github.com:meltwater/kotlin-compose.git'
        version {
            gpg{
                sign = true
            }
            released  = new Date()
        }
    }
}